pragma ton-solidity >= 0.70.0;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "./interfaces/ITranslator.tsol";
import "./interfaces/IClientReceiverContract.tsol";
import "./interfaces/IInitializerSender.tsol";
import "./interfaces/IInitializerReceiver.tsol";
import "./base/AsterizmStructs.tsol";
import "./base/AsterizmErrors.tsol";
import "./base/AsterizmTransferFlags.tsol";
import "./base/AsterizmEnvs.tsol";
import "./base/AsterizmOwnable.tsol";
import "./libs/AddressLib.tsol";
import "./libs/BytesLib.tsol";
import "./libs/UintLib.tsol";
import "./base/AsterizmInitializerTransfer.tsol";
import "./base/AsterizmConfig.tsol";

contract AsterizmInitializer is AsterizmOwnable, IInitializerSender, IInitializerReceiver, AsterizmStructs, AsterizmConfig {

    using AddressLib for address;
    using BytesLib for bytes;
    using UintLib for uint;

    /// Set translator event
    /// @param _translatorAddress address
    event SetTranslatorEvent(address _translatorAddress);

    /// Block address event
    /// @param _chainId uint64
    /// @param _address uint
    event AddBlockAddressEvent(uint64 _chainId, uint _address);

    /// Remove block address event
    /// @param _chainId uint64
    /// @param _address uint
    event RemoveBlockAddressEvent(uint64 _chainId, uint _address);

    /// Asterizm transfet hash contract deploy event
    /// @param _transferHash uint
    /// @param _address address
    event TransferHashContractDeployEvent(uint _transferHash, address _address);

    /// Init transfer successfully event
    /// @param _transferHash uint
    event InitTransferSuccessfullyEvent(uint _transferHash);

    /// Update chains list event
    event UpdateChainsListEvent();

    /// Sent payload event
    /// @param _transferHash uint  Transfer hash
    event SentPayloadEvent(uint _transferHash, address _dstAddress);

    /// Transfer error event
    /// @param _errorCode uint16
    event TransferErrorEvent(uint16 _errorCode);

    uint64 private localChainId;
    mapping(uint64 => mapping(uint => bool)) public blockAddresses;
    mapping(uint64 => AsterizmChain) private chains;

    ITranslator static translatorLib_;
    TvmCell static initializerTransferCode_;
    TvmCell static clientTransferCode_;

    constructor () {
        require(deployer_.value != 0, AsterizmErrors.TRANSFER__WRONG_WALLET_OWNER__ERROR);
        tvm.accept();

        _setOwner(deployer_);
        emit SetTranslatorEvent(address(translatorLib_));

        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        deployer_.transfer({
            value: 0,
            flag: AsterizmTransferFlags.ALL_NOT_RESERVED,
            bounce: false
        });
    }

    /// On bounce
    /// @param _slice TvmSlice
    onBounce(TvmSlice _slice) external view {
        uint32 functionId = _slice.load(uint32);
        if (functionId == tvm.functionId(translatorLib_.sendMessage)) {
            emit TransferErrorEvent(AsterizmErrors.INITIALIZER__RELAY_SEND_MESSAGE__ERROR);
        }
    }

    /** Modifiers */

    /// Only translator modifier
    modifier onlyTranslator() {
        require(msg.sender == address(translatorLib_), AsterizmErrors.INITIALIZER__ONLY_TRANSLATOR__ERROR);
        _;
    }

    /// Only translator modifier
    modifier onlyTranslatorOrExternalRelay() {
        require(msg.sender == address(translatorLib_) || getRelayData(msg.sender).externalRelayExists, AsterizmErrors.INITIALIZER__ONLY_TRANSLATOR_OR_EXTERNAL_RELAY__ERROR);
        _;
    }

    /// Send residue modifier
    modifier sendResidue() {
        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        _;
        msg.sender.transfer({
            value: 0,
            flag: AsterizmTransferFlags.ALL_NOT_RESERVED,
            bounce: false
        });
    }


    /*******************/
    /** Internal logic */
    /*******************/

    /** External contracts */

    /// Return transfer hash contract state init
    /// @param _transferHash uint  Transfer hash
    /// @return TvmCell  Transfer hash contract state init
    function _getTransferHashContractStateInit(uint _transferHash) private view returns(TvmCell) {
        return tvm.buildStateInit({
            contr: AsterizmInitializerTransfer,
            varInit: {
                deployer_: address(this),
                hash_: _transferHash
            },
            pubkey: 0,
            code: initializerTransferCode_
        });
    }

    /// Deploy transfer hash contract
    /// @param _transferHash uint  Transfer hash
    /// @return address  Transfer hash contract address
    function _deployTransferHashContract(uint _transferHash) private view returns(address) {
        address contractAddress = new AsterizmInitializerTransfer{
            stateInit: _getTransferHashContractStateInit(_transferHash),
            value: AsterizmEnvs.INITIALIZER__TRANSFER_CONTRACT_DEPLOY_VALUE,
            wid: address(this).wid,
            flag: AsterizmTransferFlags.SENDER_PAYS_FEES
        }();
        emit TransferHashContractDeployEvent(_transferHash, contractAddress);

        return contractAddress;
    }

    /// Return initializer transfer contract
    /// @param _transferHash uint  Transfer hash
    /// @return AsterizmInitializerTransfer  Transfer hash contract
    function _getTransferHashContract(uint _transferHash) private view returns(AsterizmInitializerTransfer) {
        return AsterizmInitializerTransfer(_getTransferHashContractAddress(_transferHash));
    }

    /// Return transfer hash contract address
    /// @param _transferHash uint  Transfer hash
    /// @return address  Transfer hash contract address
    function _getTransferHashContractAddress(uint _transferHash) private view returns(address) {
        return address(tvm.hash(_getTransferHashContractStateInit(_transferHash)));
    }

    /** Other internal functions */

    /// Block address
    /// @param _chainId uint64  Chain id
    /// @param _address uint  Available flag
    function addBlockAddress(uint64 _chainId, address _address) external onlyOwner sendResidue {
        blockAddresses[_chainId][_address.toUint()] = true ;
        emit AddBlockAddressEvent(_chainId, _address.toUint());
    }

    /// Unblock address
    /// @param _chainId uint64 Chain id
    function removeBlockAddress(uint64 _chainId, address _address) external onlyOwner sendResidue {
        require(blockAddresses[_chainId][_address.toUint()], AsterizmErrors.CLIENT__ADDRESS_IS_NOT_BLOCKED__ERROR);
        delete blockAddresses[_chainId][_address.toUint()];
        emit RemoveBlockAddressEvent(_chainId, _address.toUint());
    }

    /// Update chains list
    function updateChainsList() external view onlyOwner sendResidue {
        translatorLib_.getChainsList{
            value: AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE,
            callback: onUpdateChainsListCallback
        }();
    }

    /// Update chains list callback
    function onUpdateChainsListCallback(mapping(uint64 => AsterizmChain) _chains) public onlyTranslator {
        chains = _chains;
        emit UpdateChainsListEvent();

        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        owner().transfer({
            value: 0,
            flag: AsterizmTransferFlags.ALL_NOT_RESERVED,
            bounce: false
        });
    }

    /// Update local chains id
    function updateLocalChainId() external view onlyOwner sendResidue {
        translatorLib_.getLocalChainId{
            value: AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE,
            callback: onUpdateLocalChainIdCallback
        }();
    }

    /// Update local chains id callback
    function onUpdateLocalChainIdCallback(uint64 _localChainId) public onlyTranslator {
        localChainId = _localChainId;

        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        owner().transfer({
            value: 0,
            flag: AsterizmTransferFlags.ALL_NOT_RESERVED,
            bounce: false
        });
    }

    /// Withdraw excess coins to owner
    function withdrawCoins() external onlyOwner {
        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        owner().transfer({ value: 0, flag: AsterizmTransferFlags.ALL_NOT_RESERVED, bounce: false });
    }


    /*******************/
    /** External logic */
    /*******************/

    /** Other external functions */

    /// Return local chain id
    /// @return uint64
    function getLocalChainId() external internalMsg view responsible override returns(uint64) {
        return { value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false } localChainId;
    }

    /// Return initializer transfer code
    /// @return TvmCell
    function getInitializerTransferCode() external internalMsg view responsible override returns(TvmCell) {
        return { value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false } initializerTransferCode_;
    }

    /// Return client transfer code
    /// @return TvmCell
    function getClientTransferCode() external internalMsg view responsible override returns(TvmCell) {
        return { value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false } clientTransferCode_;
    }

    /// Return chains list
    /// @return mapping(uint64 => AsterizmChain)
    function getChainsList() external internalMsg view responsible override returns(mapping(uint64 => AsterizmChain)) {
        return { value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false } chains;
    }


    /** Sending logic */

    /// Initiate asterizm transfer
    /// @param _dto IzIninTransferRequestDto  Method DTO
    function initTransfer(IzIninTransferRequestDto _dto) external override {
        address sender = msg.sender;

        require(!blockAddresses[localChainId][sender.toUint()], AsterizmErrors.INITIALIZER__SENDER_ADDRESS_IS_BLOCKED__ERROR);
        require(!blockAddresses[_dto.dstChainId][_dto.dstAddress], AsterizmErrors.INITIALIZER__TARGET_ADDRESS_IS_BLOCKED__ERROR);

        _initTransferPrivate(_dto, sender);
    }

    /// Init transfer (private)
    /// @param _dto IzIninTransferRequestDto  Method DTO
    /// @param _sender address  Sender (cilent) address
    function _initTransferPrivate(IzIninTransferRequestDto _dto, address _sender) private view {
        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        TrSendMessageRequestDto dto = _buildTrSendMessageRequestDto(
            _sender.toUint(), _dto.dstChainId, _dto.dstAddress, _dto.txId,
            _dto.transferHash, _dto.dstFeeAmount, _dto.transferResultNotifyFlag
        );

        if (_dto.relay != address(0) && _dto.relay != address(translatorLib_)) { // External relays logic
            ConfigDataResponseDto configDto = getRelayData(_dto.relay);
            if (configDto.externalRelayExists) {
                uint128 logFee = 0.1 ton;
                require(msg.value > configDto.systemFee + configDto.externalRelayFee + logFee, AsterizmErrors.INITIALIZER__FEE_NOT_ENOUGH__ERROR);
                translatorLib_.logExternalMessage{ value: configDto.systemFee + logFee, flag: AsterizmTransferFlags.SENDER_PAYS_FEES, bounce: false }(_dto.relay, dto);
                ITranslator(_dto.relay).sendMessage{ value: 0, flag: AsterizmTransferFlags.ALL_NOT_RESERVED, bounce: false }(dto);
                return;
            }
        }

        emit InitTransferSuccessfullyEvent(_dto.transferHash);
        translatorLib_.sendMessage{
            value: 0,
            flag: AsterizmTransferFlags.ALL_NOT_RESERVED,
            bounce: true
        }(dto);
    }

    /// Resend failed by fee amount transfer
    /// @param _transferHash uint  Transfer hash
    /// @param _feeAmount uint128  Transfer fee amount
    /// @param _relay address  External relay address
    function resendTransfer(uint _transferHash, uint128 _feeAmount, address _relay) external view {
        require(_feeAmount < msg.value, AsterizmErrors.INITIALIZER__TRANSFER_FEE_VALUE_IS_TOO_SMALL_ERROR);
        require(address(this).balance >= _feeAmount, AsterizmErrors.INITIALIZER__BALANCE_NOT_ENOUGH__ERROR);
        address sender = msg.sender;

        if (_relay != address(0) && _relay != address(translatorLib_)) { // External relays logic
            ConfigDataResponseDto configDto = getRelayData(_relay);
            if (configDto.externalRelayExists) {
                ITranslator(_relay).resendMessage{ value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false }(_transferHash, sender.toUint(), _feeAmount);
                return;
            }
        }

        translatorLib_.resendMessage{ value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false }(_transferHash, sender.toUint(), _feeAmount);
    }

    /// Transfer sending result notification
    /// @param _targetAddress address  Target client contract address
    /// @param _transferHash bytes32  Transfer hash
    /// @param _statusCode uint8  Status code
    function transferSendingResultNotification(address _targetAddress, bytes32 _transferHash, uint8 _statusCode) external view onlyTranslatorOrExternalRelay {
        IClientReceiverContract(_targetAddress).transferSendingResultNotification{
            value: AsterizmEnvs.INITIALIZER__NOTIFY_TRANSFER_STATUS_VALUE,
            flag: AsterizmTransferFlags.RECEIVER_PAYS_FEES,
            bounce: false
        }(_transferHash, _statusCode);
    }


    /** Receiving logic */

    /// Receive payload from translator
    /// @param _dto IzReceivePayloadRequestDto  Method DTO
    function receivePayload(IzReceivePayloadRequestDto _dto) external internalMsg override onlyTranslatorOrExternalRelay {
        require(msg.value >= AsterizmEnvs.INITIALIZER__MIN_RECEIVE_VALUE, AsterizmErrors.INITIALIZER__NOT_ENOUGH_COINS__ERROR);
        require(!blockAddresses[localChainId][_dto.dstAddress], AsterizmErrors.INITIALIZER__SENDER_ADDRESS_IS_BLOCKED__ERROR);

        _receivePayloadPrivate(_dto);
    }

    /// Receive payload (private)
    /// @param _dto IzReceivePayloadRequestDto  Method DTO
    function _receivePayloadPrivate(IzReceivePayloadRequestDto _dto) private view {
        tvm.rawReserve(AsterizmEnvs.INITIALIZER__MIN_CONTRACT_BALANCE, 0);
        IzAsterizmReceiveRequestDto dto = _buildIzAsterizmReceiveRequestDto(
            _dto.srcChainId, _dto.srcAddress, _dto.dstChainId,
            _dto.dstAddress, _dto.txId, _dto.transferHash
        );

        _deployTransferHashContract(_dto.transferHash);
        _getTransferHashContract(_dto.transferHash).initialize();

        IClientReceiverContract(_dto.dstAddress.toAddress()).asterizmIzReceive{
            value: AsterizmEnvs.INITIALIZER__RECEIVE_TRANSFER_CLIENT_CONTRACT_VALUE,
            flag: AsterizmTransferFlags.RECEIVER_PAYS_FEES,
            bounce: false
        } (dto);

        emit SentPayloadEvent(_dto.transferHash, _dto.dstAddress.toAddress());
        owner().transfer({ value: 0, flag: AsterizmTransferFlags.ALL_NOT_RESERVED, bounce: false });
    }
}
