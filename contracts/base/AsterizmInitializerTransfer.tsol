// SPDX-License-Identifier: MIT
pragma ton-solidity >= 0.70.0;

import "../interfaces/IInitializerTransfer.tsol";
import "./AsterizmEnvs.tsol";
import "./AsterizmTransferFlags.tsol";
import "./AsterizmOwnable.tsol";
import "../libs/UintLib.tsol";

contract AsterizmInitializerTransfer is IInitializerTransfer, AsterizmOwnable {

    using UintLib for uint;

    /// Deploy contract event
    /// @param _selfAddress address
    event DeployInitializerTransferContractEvent(address _selfAddress);

    uint static hash_;

    bool private isInitialized;

    constructor() {
        require(tvm.pubkey() == 0, AsterizmErrors.TRANSFER__NON_ZERO_PUBLIC_KEY__ERROR);
        require(deployer_.value != 0, AsterizmErrors.TRANSFER__WRONG_WALLET_OWNER__ERROR);
        require(deployer_ == msg.sender, AsterizmErrors.TRANSFER__WRONG_OWNER__ERROR);

        emit DeployInitializerTransferContractEvent(address(this));

        tvm.rawReserve(AsterizmEnvs.TRANSFER__MIN_CONTRACT_BALANCE, 0);
        deployer_.transfer({ value: 0, flag: AsterizmTransferFlags.ALL_NOT_RESERVED, bounce: false });
    }

    /// Only initialized modifier
    modifier onlyInitialized {
        require(isInitialized, AsterizmErrors.TRANSFER__NOT_INITIALISED__ERROR);
        _;
    }

    // Initialize contract after deploy
    function initialize() external onlyDeployer {
        _setOwner(msg.sender);
        isInitialized = true;
    }

    /// Is transfer exists
    /// @param _smallTransferHash uint128
    /// @return uint, bool, uint8
    function isExists(uint128 _smallTransferHash) external internalMsg view responsible override onlyInitialized returns(uint, bool, uint8) {
        uint baseHash = hash_;
        require(baseHash.toUint128() == _smallTransferHash, AsterizmErrors.TRANSFER__WRONG_HASH__ERROR);
        return { value: 0, flag: AsterizmTransferFlags.REMAINING_GAS, bounce: false } (hash_, true, AsterizmEnvs.METHOD__INITIALIZER_HASH_EXISTS);
    }
}
